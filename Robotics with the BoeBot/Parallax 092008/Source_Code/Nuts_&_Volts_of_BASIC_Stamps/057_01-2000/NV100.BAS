'Program Listing 59.1: NV100.BAS Interfacing to the MAX144 12-Bit ADC
SYMBOL 	CS_AD		= 0
SYMBOL		SDATA		= 1
SYMBOL		CLK		= 2
SYMBOL		DATpin		= pin1
SYMBOL		Ad_flag		= bit12

SYMBOL		CLOCKS 	= B2
SYMBOL		SAMPLES 	= B3
SYMBOL		AD0_CAL 	= W0
SYMBOL		AD1_IN 		= W2
SYMBOL		WORK 		= W3
SYMBOL		AD_RESULT 	= W4

BEGIN:
	LET DIRS = %11111101
	PAUSE	1000
MAIN:
	GOSUB	ANALOG
	GOTO	MAIN

' ***************************************************
ANALOG:
	HIGH	CS_AD
	HIGH	CLK
	AD_RESULT = 0
	FOR SAMPLES = 1 TO 16
		LOW 	CLK
		LOW 	CS_AD
		LET AD0_CAL = 0
		FOR CLOCKS = 1 TO 16
			LET AD0_CAL = AD0_CAL * 2
			LET AD0_CAL = AD0_CAL + DATpin
			PULSOUT CLK,10
		NEXT
		IF	BIT12 = 1 THEN ANALOG
		AD0_CAL = AD0_CAL&$0FFF
		HIGH	CS_AD
		HIGH	CLK
		LOW 	CLK
		LOW 	CS_AD
		LET AD1_IN = 0
		FOR CLOCKS = 1 TO 16
			LET AD1_IN = AD1_IN * 2
			LET AD1_IN = AD1_IN + DATpin
			PULSOUT CLK,10
		NEXT
		AD1_IN = AD1_IN&$0FFF
		HIGH	CS_AD
		HIGH	CLK
		WORK = AD1_IN + 2500 - AD0_CAL 
		AD_RESULT = AD_RESULT + WORK
	NEXT
	AD_RESULT = AD_RESULT/16
	DEBUG	AD_RESULT,CR,CR
	RETURN
'**************************************************
END:
