' Program Listing 21.2. BS2 Program to Demonstrate TSL230
' Program: TSL230.BS2 (Interface with TSL230 light sensor)
' This program demonstrates the light-to-frequency conversion
' capability of the TSL230 sensor from Texas Instruments.
' BS1 pins 0 and 1 control the sensitivity of the '230 through
' its "electronic aperture" feature. The higher the sensitivity,
' the higher the frequency output for a given light intensity,
' as shown below:
' 	bit1 	bit0 	Sensitivity
' 	---- 	---- 	-----------
' 	0 	0 	sensor OFF
' 	0 	1 	x1
' 	1 	0 	x10
' 	1 	1 	x100
' This BS2 program uses the COUNT instruction to count the number
' of TSL230 output cycles over a period of 1 second. This approach
' trades a relatively long measurement period for excellent
' resolution and accuracy. For a quick-and-dirty measurement,
' the PULSIN approach used with the BS1 could be employed instead.
' The program displays its readings on a 2x16 serial LCD module.

sens 	var byte 	' Sensitivity setting.
mult 	var byte 	' Multiplier for a given sensitivity.
light 	var word 	' Light-intensity reading.
I 	con 254 	' Instruction prefix for LCD.
one 	con 128 	' Address of 1st LCD line.
two 	con 192 	' Address of 2nd LCD line.
n2400 	con $418d 	' Serial constant for 2400 bps

dirs = %00000011 	' Make pins 0 and 1 outputs.

again: 			' Main program loop.
for sens = 1 to 3 	' Walk through sensitivity settings.
  OUTS = sens 		' Write sensitivity setting to pins.
  lookup sens,[0,1,10,100],mult ' Get the sensitivity multiplier.
  count 2,1000,light 	' Count pulses for 1 second.
  Serout 7,n2400,[I,one,"multiplier: x",DEC mult," "] ' Display.
  serout 7,n2400,[I,two,"light: ",DEC light," "]
next 			' Next sensitivity.
goto again 		' Repeat forever.
