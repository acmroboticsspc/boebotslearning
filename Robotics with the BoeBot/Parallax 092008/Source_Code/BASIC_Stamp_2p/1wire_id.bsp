' -----[ Title ]------------------------------------------------------
'
' File...... 1wire_id.bsp
' Purpose... Reading Family Code and Serial Number for Identification 
' Author.... Claus Kuehnel  
' Started...  
' Updated... 

' -----[ Program Description ]----------------------------------------
'
' 

' -----[ Revision History ]-------------------------------------------
'

' -----[ Directives ]-------------------------------------------------
'
'{$STAMP BS2p} 			'specifies a BS2p

' -----[ Constants ]--------------------------------------------------
'
OWpin		con 15	'1-wire device pin

OW_FERst	con %0001	'Front-End Reset
OW_BERst	con %0010	'Back-End reset
OW_BitMode	con %0100	

ReadROM	con $33	'Read ROM Command
SearchROM	con $F0	'Search ROM Comand
 
NoDevice	con %11	'No device present
     
' -----[ Variables ]--------------------------------------------------
'
idx		var byte
ROMData	var byte(8)
value		var byte
CRC		var byte
index		var byte
i		var byte
devcheck	var nib
    

' -----[ Initialization ]---------------------------------------------
'
init:	pause 1000		'open debug window

' -----[ Main Code ]--------------------------------------------------
'
main:	debug cls
	gosub devicecheck
	if (devcheck <> NoDevice) then displayROM

NoDeviceFound:
	debug "No 1-Wire device present."
	pause 1000
	goto main

displayROM:
	debug "Dallas 1-Wire ID: "
	owout OWpin, OW_FERst, [ReadROM]
	owin  OWpin, OW_BERst, [str ROMData\8]
	CRC = 0
	for idx = 0 to 7
		debug hex2 ROMData(idx), " "
	next
	for idx = 0 to 6
		value = ROMData(idx)
		gosub CRC8
	next
	debug cr, "CRC = ", hex2 CRC
	if CRC = ROMData(7) then nxt
	debug cr, "CRC not OK." : goto nxt1
nxt:	debug cr, "CRC OK."
nxt1:	pause 1000
	goto main

' -----[ Subroutines ]------------------------------------------------
'
devicecheck:
	devcheck = 0
	owout OWpin, OW_FERst, [SearchROM]
	owin  OWpin, OW_BitMode, [devcheck.bit1, devcheck.bit0]
	return

CRC8:	restore
	index = CRC ^ value
	for i = 0 to index
		read i, CRC
	next
	return

' -----[ Data ]-------------------------------------------------------
'
data 0, 94, 188, 226, 97, 63, 221, 131, 194, 156, 126, 32, 163, 253, 31, 65
data 157, 195, 33, 127, 252, 162, 64, 30, 95, 1, 227, 189, 62, 96, 130, 220
data 35, 125, 159, 193, 66, 28, 254, 160, 225, 191, 93, 3, 128, 222, 60, 98
data 190, 224, 2, 92, 223, 129, 99, 61, 124, 34, 192, 158, 29, 67, 161, 255
data 70, 24, 250, 164, 39, 121, 155, 197, 132, 218, 56, 102, 229, 187, 89, 7
data 219, 133, 103, 57, 186, 228, 6, 88, 25, 71, 165, 251, 120, 38, 196, 154
data 101, 59, 217, 135, 4, 90, 184, 230, 167, 249, 27, 69, 198, 152, 122, 36
data 248, 166, 68, 26, 153, 199, 37, 123, 58, 100, 134, 216, 91, 5, 231, 185
data 140, 210, 48, 110, 237, 179, 81, 15, 78, 16, 242, 172, 47, 113, 147, 205
data 17, 79, 173, 243, 112, 46, 204, 146, 211, 141, 111, 49, 178, 236, 14, 80
data 175, 241, 19, 77, 206, 144, 114, 44, 109, 51, 209, 143, 12, 82, 176, 238
data 50, 108, 142, 208, 83, 13, 239, 177, 240, 174, 76, 18, 145, 207, 45, 115
data 202, 148, 118, 40, 171, 245, 23, 73, 8, 86, 180, 234, 105, 55, 213, 139
data 87, 9, 235, 181, 54, 104, 138, 212, 149, 203, 41, 119, 244, 170, 72, 22
data 233, 183, 85, 11, 136, 214, 52, 106, 43, 117, 151, 201, 74, 20, 246, 168
data 116, 42, 200, 150, 21, 75, 169, 247, 182, 232, 10, 84, 215, 137, 107, 53

