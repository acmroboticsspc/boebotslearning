' -----[ Title ]------------------------------------------------------
'
' File...... ds1994_mem.bsp
' Purpose... Storing data in Memory of DS1994 iButton  
' Author.... Claus Kuehnel  
' Started... 2001-08-11 
' Updated... 

' -----[ Program Description ]----------------------------------------
'
' This program demonstrates the writing and reading DS1994
' memory via the internal ScratchPad RAM
 

' -----[ Revision History ]-------------------------------------------
'
' 2001-08-11

' -----[ Directives ]-------------------------------------------------
'
'{$STAMP BS2p} 			'specifies a BS2p

' -----[ Constants ]--------------------------------------------------
'
OWpin		con 15	'1-wire device pin
LED		con 12	'Pin for LED

OW_FERst	con %0001	'Front-End Reset
OW_BERst	con %0010	'Back-End reset
OW_BitMode	con %0100	

SkipROM	con $CC	'Skip ROM Command
WriteScratch con $0F	'Write ScratchPad
ReadScratch con $AA	'Read ScratchPad
CopyScratch con $55	'Copy ScratchPad to Memory
ReadMemory	con $F0	'Read Memory

cTargetAddr	con $0077	'Target Address in Memory

     
' -----[ Variables ]--------------------------------------------------
'
TargetAddr 	var word
TA1 		var TargetAddr.LowByte
TA2		var TargetAddr.HighByte
EndingAddr	var byte

temp 		var byte(8)
idx 		var nib
    

' -----[ Initialization ]---------------------------------------------
'
init:	pause 1000		'open debug window
	low LED		'LED off
	
	

' -----[ Main Code ]--------------------------------------------------
'
Start:'Initialize array
	for idx = 0 to 7
		temp(idx) = idx +$30
	next
	debug cls,"Array contains ", str temp\8, cr
	
	TargetAddr = cTargetAddr
	debug "TA:   ", hex2 TA2, hex2 TA1, cr

	'Write array to ScratchPad RAM
	high LED
	debug "Write to ScratchPad RAM...",cr
	OWOUT OWpin, OW_FERst, [SkipROM, WriteScratch, TA1, TA2, str temp\8 ]
	low LED

	'Clear array
	debug "Clear ScrachPad RAM...",cr
	for idx = 0 to 7
		temp(idx) = $96
	next
	debug "Array contains ", str temp\8, cr

	'Read scratchpad RAM and save back into array
	debug "Read ScratchPad RAM...",cr
	OWOUT OWpin, OW_FERst, [SkipROM, ReadScratch]
	OWIN OWpin, OW_BERst, [TA1, TA2, EndingAddr, str temp\8 ]

	debug "TA:   ", hex2 TA2, hex2 TA1, cr
	debug "EA:   ", hex2 EndingAddr, cr
	debug "Data: ", str temp\8, cr

	'Clear array again
	debug "Clear ScrachPad RAM...",cr
	for idx = 0 to 7
		temp(idx) = $96  ' any bit pattern
	next
	debug "Array contains ", str temp\8, cr

	'Copy ScratchPad to Memory
	debug "Copy ScratchPad to Memory...", cr
	OWOUT OWpin, OW_FERst, [SkipROM, CopyScratch, TA1, TA2, EndingAddr]

	'Read Memory and save back into array
	debug "Read Memory...", cr
	OWOUT OWpin, OW_FERst, [SkipROM, ReadMemory, TA1, TA2]
	OWIN OWpin, OW_BERst, [str temp\8 ]	
	debug "Array contains ", str temp\8, cr

	pause 5000
	goto start

	end

' -----[ Subroutines ]------------------------------------------------

' -----[ Data ]-------------------------------------------------------